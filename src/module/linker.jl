# Linking of different PTX modules

export
    CuLink, complete, destroy,
    addData, addFile


typealias CuLinkState_t Ptr{Void}

immutable CuLink
    handle::CuLinkState_t

    options::Dict{CUjit_option,Any}
    optionKeys::Vector{CUjit_option}
    optionVals::Vector{Ptr{Void}}

    function CuLink()
        handle_ref = Ref{CuLinkState_t}()

        options = Dict{CUjit_option,Any}()
        options[ERROR_LOG_BUFFER] = Array(UInt8, 1024*1024)
        @static if DEBUG
            options[GENERATE_LINE_INFO] = true
            options[GENERATE_DEBUG_INFO] = true

            options[INFO_LOG_BUFFER] = Array(UInt8, 1024*1024)
            options[LOG_VERBOSE] = true
        end
        optionKeys, optionVals = encode(options)

        @apicall(:cuLinkCreate, (Cuint, Ptr{CUjit_option}, Ptr{Ptr{Void}}, Ptr{CuLinkState_t}),
                                length(optionKeys), optionKeys, optionVals, handle_ref)

        new(handle_ref[], options, optionKeys, optionVals)
    end
end

Base.unsafe_convert(::Type{CuLinkState_t}, link::CuLink) = link.handle

Base.:(==)(a::CuLink, b::CuLink) = a.handle == b.handle
Base.hash(link::CuLink, h::UInt) = hash(link.handle, h)

"""
Complete a pending linker invocation, returning an output image.

Note that this image is still owned by the JIT, meaning it will be destroyed upon calling `destroy` on the linker.
"""
function complete(link::CuLink)
    cubin_ref = Ref{Ptr{Void}}()
    size_ref = Ref{Csize_t}()

    try
        @apicall(:cuLinkComplete, (CuLinkState_t, Ptr{Ptr{Void}}, Ptr{Csize_t}),
                                  link, cubin_ref, size_ref)
    catch err
        (err == ERROR_NO_BINARY_FOR_GPU || err == ERROR_INVALID_IMAGE) || rethrow(err)
        options = decode(link.optionKeys, link.optionVals)
        rethrow(CuError(err.code, options[ERROR_LOG_BUFFER]))
    end

    @static if DEBUG
        options = decode(link.optionKeys, link.optionVals)
        if isempty(options[INFO_LOG_BUFFER])
            debug("JIT info log is empty")
        else
            debug("JIT info log: ", repr_indented(options[INFO_LOG_BUFFER]))
        end
    end

    return unsafe_wrap(Array, convert(Ptr{UInt8}, cubin_ref[]), size_ref[])
end

"""
Destroys state for a JIT linker invocation.

Note that this destroys any existing references to images generated by this JIT, despite having live Julia objects pointing to them.
"""
function destroy(link::CuLink)
    @apicall(:cuLinkDestroy, (CuLinkState_t,), link)
end

function addData(link::CuLink, name::String, data::Union{Vector{UInt8},String}, typ::CUjit_input)
    # NOTE: ccall can't directly convert String to Ptr{Void}, so step through a typed Ptr
    if typ == PTX
        # additionally, in the case of PTX there shouldn't be any embedded NULLs
        raw_data = Base.unsafe_convert(Cstring, Base.cconvert(Cstring, String(data)))
    else
        raw_data = Base.unsafe_convert(Vector{UInt8}, Base.cconvert(Vector{UInt8}, data))
    end
    typed_ptr = pointer(raw_data)
    untyped_ptr = convert(Ptr{Void}, typed_ptr)

    @apicall(:cuLinkAddData,
             (CuLinkState_t, CUjit_input, Ptr{Void}, Csize_t, Cstring, Cuint, Ptr{CUjit_option}, Ptr{Ptr{Void}}),
             link, typ, untyped_ptr, length(data), name, 0, C_NULL, C_NULL)

    return nothing
end

function addFile(link::CuLink, path::String, typ::CUjit_input)
    @apicall(:cuLinkAddFile,
             (CuLinkState_t, CUjit_input, Cstring, Cuint, Ptr{CUjit_option}, Ptr{Ptr{Void}}),
             link, typ, path, 0, C_NULL, C_NULL)

    return nothing
end
